#!/usr/bin/env perl
=pod

=head1 NAME

mysqlcheck -- check mysql alive with ping method.

=head1 SYNOPSIS

Usage: mysqlcheck [OPTION...]

=head1 RISKS

=cut

# cz 20170524

use strict;
use warnings;
use Getopt::Long;
use DBI;
use DBD::mysql;
use Data::Dumper;
use Getopt::Long;
use POSIX qw(strftime);
use LWP::UserAgent;
use HTTP::Request::Common;
use Sys::Hostname;
use MIME::Base64;
use JSON;
use Net::Ping;
use Config::IniFiles;
use IO::Socket::INET;

my($conf,$verbose,$tag,$night,$help);
my $consul  = "localhost:8500";
my $sshport = 22;
my $token;
GetOptions(
  "conf=s"   => \$conf,
  "verbose!" => \$verbose,
  "tag=s"    => \$tag,
  "consul=s" => \$consul,
  "token=s"  => \$token,
  "night!"   => \$night,
  "sshport=i"=> \$sshport,
  "help!"    => \$help,
) or die "error:$!";

if ($help) {
  system("perldoc $0");
  exit(0);
}

# get database file handle.
$conf = "./$conf" if $conf && $conf =~ /^[^\/]/;
sub get_dbh{
  my $group = shift;
  my $dsn = "DBI:mysql:mysql_connect_timeout=1;"
          . "mysql_read_default_file=$conf;"
          . "mysql_read_default_group=$group";
  
  my $dbh = DBI->connect("$dsn", undef, undef);
  return $dbh;
}

# get current key value
sub get_curr_key {
    my ( $ua, $tag, $key )   = @_;
    my $keyurl = "http://$consul/v1/kv/mysql/$tag/$key"
               . do { defined($token)
                      ? "?token=$token"
                      : ""
                    }
               ;

    # get key value
    my $res = $ua->get("$keyurl");
    unless ($res->is_success) {
       return;
    }

    my $decode = $res->decoded_content;
    $decode    =~ s/(?:^\[|\]$)//g;
    my $text = decode_json($decode);

    my $value = defined($text->{'Value'})
              ? decode_base64($text->{'Value'})
              : undef ;
    return $value;
}

# set alive key value
sub set_alive_key {
  my ( $ua, $tag, $key, $value) = @_;
  my $keyurl = "http://$consul/v1/kv/mysql/$tag/$key"
             . do { defined($token)
                    ? "?token=$token"
                    : ""
                  }
             ;
  # post key value
  my $request  = 
    HTTP::Request::Common::PUT(
       $keyurl,
       'User-Agent' => 'memcheck_v0.1',
       'Content' => "$value"
    );
  my $res = $ua->request($request);
  #print Dumper($res);
  unless ($res->is_success) {
    return 0;
  }
  my $decode = $res->decoded_content;
  if ($decode eq 'true') {
    return "ok";
  }
  return;
}

sub get_ip{
  my $cfg = Config::IniFiles->new( -file => "$conf" );
  return $cfg->val( "$tag", 'host');
}

sub ping_is_alive {
  my $ip = shift;
  my $t = 0;
  my $n = 3;
  # check 3 times
  my $p  = Net::Ping->new("icmp", 1);
  while($n && $n--) {
    my $ping_ok = $p->ping($ip);
    $t += 1 if $ping_ok;
    select(undef, undef, undef, 0.2);
  }
  if ($t > 0) {
    echo_msg("ping host $ip is reachable");
    return 1;
  }
  else {
    echo_msg("ping host $ip is not reachable");
    return 0;
  }
}

sub ssh_is_alive {
  my $ip = shift;
  my $t = 0;
  my $n = 2;

  while($n && $n--) {
    my $socket = IO::Socket::INET->new(
      PeerAddr => $ip,
      PeerPort => $sshport,
      Proto    => 'tcp',
      Timeout  => 2
    ) || $t++;
  }
  if ($t <= 1) {
    echo_msg("ssh host $ip:$sshport is reachable");
    return 1;
  }
  else {
    echo_msg("ssh host $ip:$sshport is not reachable");
    return 0;
  }
}

sub night_check_skip {
  my @tm = localtime(time);
  if ($tm[2] >= 8 && $tm[2] <= 21) {
    echo_msg("only switch in night 22:00 ~ 08:00, "
             . "you can disbale with no option --night");
    return 1;
  }
  return 0;
}

sub get_time {
  return 
    strftime("%Y-%m-%dT%H:%M:%S", localtime(time));
}

sub echo_msg {
  my $msg = shift;
  my $now = get_time();
  print "[$now] $msg\n";
}

my $host= hostname;

# http client
my $ua = LWP::UserAgent->new;
$ua->timeout(3);

$| = 1;

while(1) {
  my $dbh = get_dbh("$tag") || undef;
  my $rc  = defined($dbh)
          ? $dbh->ping()
          : 0;
  my $ip  = get_ip($dbh);
  eval {
    my $n = 3;
    my $value = get_curr_key($ua, $tag, $host);
    while(!defined($value) && $n && $n--) {
      $value = get_curr_key($ua, $tag, $host);
      $value ||= 2 if $n == 0; # except value;
    }
    if ($rc == 0) {
      # only send 0 in night
      if($night && night_check_skip) {
        $rc = 1;
      }
      # if host is down, skip the nigth check
      unless((ssh_is_alive($ip) 
               && ping_is_alive($ip))) {
        $rc = 0;
      }
    }
    if ($value == $rc) {
      echo_msg("mysql/$tag/$host with value $value no change");
    } else {
      if ( set_alive_key($ua, $tag, $host, $rc) ) {
        echo_msg("set $rc with key mysql/$tag/$host ok");
      }
      else {
        echo_msg("set $rc with key mysql/$tag/$host error");
      }
    }
    
  };

  if ($@) {
    echo_msg("error - set key: $!") if $verbose;
  }
  sleep(1);
}

# ###################################################################
# Documentation.
# ###################################################################

=head1 OPTIONS

=over 4

=item conf

database config file, depend on DBD::mysql mysql_read_default_group
method, eg:

    [mysql3308]
    database=information_schema
    host=10.0.1.1
    port=3308
    user=dbuser
    password=xxxxxxxx
    default-character-set=utf8

=item tag

instance name from config file. such as mysql3308 with conf option.

=item consul

consul http address, default is "localhost:8500"

=item token

consul acl_token, default is undef.

=item night

only send 0 value to consul in 09:00 ~ 20:00, but not effect when server is down.

=item sshport

check the MySQL host ssh port when MySQL abnormal, default is 22.

=item help

print help info.

=back

=head1 AUTHOR

zhe.chen <chenzhe07@gmail.com>

=head1 CHANGELOG

v0.1.0 version

=cut
